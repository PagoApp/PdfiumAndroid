plugins {
    id 'com.android.library' version '8.2.2'
    id 'maven-publish'
    id("com.jfrog.artifactory") version "5.2.5"
}

apply from: file('publish.gradle')

ext {
    tools = [
            minSdk     : 21,
            targetSdk  : 34,
            compileSdk : 34,
            versionCode: 3,
            versionName: '1.9.4'
    ]
}

group "com.github.lion1988dev"

android {
    android.libraryVariants.all { variant ->
        variant.outputs.all {
            // Rename output variant to whatever you want & it would get uploaded as library
            outputFileName = "pdfium-android-release.aar" //I.e. myLib.jar
        }
    }

    namespace "com.shockwave.pdfium"

    compileSdk rootProject.tools.compileSdk

    defaultConfig {
        minSdkVersion rootProject.tools.minSdk
        targetSdk rootProject.tools.targetSdk

        versionCode rootProject.tools.versionCode
        versionName "${rootProject.tools.versionName}"

        buildConfigField 'String', 'VERSION_NAME', "\"${defaultConfig.versionName}\""

        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    ndkVersion '28.0.12674087'

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jniLibs']
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
}
